<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Function.Endomorphism.Setoid</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Endomorphisms on a Setoid</a>
<a id="135" class="Comment">------------------------------------------------------------------------</a>

<a id="209" class="Symbol">{-#</a> <a id="213" class="Keyword">OPTIONS</a> <a id="221" class="Pragma">--cubical-compatible</a> <a id="242" class="Pragma">--safe</a> <a id="249" class="Symbol">#-}</a>

<a id="254" class="Comment">-- Disabled to prevent warnings from deprecated names</a>
<a id="308" class="Symbol">{-#</a> <a id="312" class="Keyword">OPTIONS</a> <a id="320" class="Pragma">--warn=noUserWarning</a> <a id="341" class="Symbol">#-}</a>

<a id="346" class="Keyword">open</a> <a id="351" class="Keyword">import</a> <a id="358" href="Relation.Binary.Core.html" class="Module">Relation.Binary.Core</a> <a id="379" class="Keyword">using</a> <a id="385" class="Symbol">(</a><a id="386" href="Relation.Binary.Core.html#1577" class="Function Operator">_Preserves_⟶_</a><a id="399" class="Symbol">)</a>
<a id="401" class="Keyword">open</a> <a id="406" class="Keyword">import</a> <a id="413" href="Relation.Binary.Bundles.html" class="Module">Relation.Binary.Bundles</a> <a id="437" class="Keyword">using</a> <a id="443" class="Symbol">(</a><a id="444" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a><a id="450" class="Symbol">)</a>

<a id="453" class="Keyword">module</a> <a id="460" href="Function.Endomorphism.Setoid.html" class="Module">Function.Endomorphism.Setoid</a> <a id="489" class="Symbol">{</a><a id="490" href="Function.Endomorphism.Setoid.html#490" class="Bound">c</a> <a id="492" href="Function.Endomorphism.Setoid.html#492" class="Bound">e</a><a id="493" class="Symbol">}</a> <a id="495" class="Symbol">(</a><a id="496" href="Function.Endomorphism.Setoid.html#496" class="Bound">S</a> <a id="498" class="Symbol">:</a> <a id="500" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="507" href="Function.Endomorphism.Setoid.html#490" class="Bound">c</a> <a id="509" href="Function.Endomorphism.Setoid.html#492" class="Bound">e</a><a id="510" class="Symbol">)</a> <a id="512" class="Keyword">where</a>

<a id="519" class="Keyword">open</a> <a id="524" class="Keyword">import</a> <a id="531" href="Agda.Builtin.Equality.html" class="Module">Agda.Builtin.Equality</a>

<a id="554" class="Keyword">open</a> <a id="559" class="Keyword">import</a> <a id="566" href="Algebra.html" class="Module">Algebra</a>
<a id="574" class="Keyword">open</a> <a id="579" class="Keyword">import</a> <a id="586" href="Algebra.Structures.html" class="Module">Algebra.Structures</a>
<a id="605" class="Keyword">open</a> <a id="610" class="Keyword">import</a> <a id="617" href="Algebra.Morphism.html" class="Module">Algebra.Morphism</a><a id="633" class="Symbol">;</a> <a id="635" class="Keyword">open</a> <a id="640" href="Algebra.Morphism.html#735" class="Module">Definitions</a>
<a id="652" class="Keyword">open</a> <a id="657" class="Keyword">import</a> <a id="664" href="Function.Equality.html" class="Module">Function.Equality</a>
<a id="682" class="Keyword">open</a> <a id="687" class="Keyword">import</a> <a id="694" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="708" class="Keyword">using</a> <a id="714" class="Symbol">(</a><a id="715" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="716" class="Symbol">;</a> <a id="718" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">_+_</a><a id="721" class="Symbol">);</a> <a id="724" class="Keyword">open</a> <a id="729" href="Agda.Builtin.Nat.html#186" class="Module">ℕ</a>
<a id="731" class="Keyword">open</a> <a id="736" class="Keyword">import</a> <a id="743" href="Data.Nat.Properties.html" class="Module">Data.Nat.Properties</a>
<a id="763" class="Keyword">open</a> <a id="768" class="Keyword">import</a> <a id="775" href="Data.Product.Base.html" class="Module">Data.Product.Base</a> <a id="793" class="Keyword">using</a> <a id="799" class="Symbol">(</a><a id="800" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">_,_</a><a id="803" class="Symbol">)</a>

<a id="806" class="Keyword">import</a> <a id="813" href="Relation.Binary.Indexed.Heterogeneous.Construct.Trivial.html" class="Module">Relation.Binary.Indexed.Heterogeneous.Construct.Trivial</a> <a id="869" class="Symbol">as</a> <a id="872" class="Module">Trivial</a>

<a id="881" class="Keyword">private</a>
  <a id="891" class="Keyword">module</a> <a id="E"></a><a id="898" href="Function.Endomorphism.Setoid.html#898" class="Module">E</a> <a id="900" class="Symbol">=</a> <a id="902" href="Relation.Binary.Bundles.html#1041" class="Module">Setoid</a> <a id="909" class="Symbol">(</a><a id="910" href="Function.Equality.html#2155" class="Function">setoid</a> <a id="917" href="Function.Endomorphism.Setoid.html#496" class="Bound">S</a> <a id="919" class="Symbol">(</a><a id="920" href="Relation.Binary.Indexed.Heterogeneous.Construct.Trivial.html#1333" class="Function">Trivial.indexedSetoid</a> <a id="942" href="Function.Endomorphism.Setoid.html#496" class="Bound">S</a><a id="943" class="Symbol">))</a>
  <a id="948" class="Keyword">open</a> <a id="953" href="Function.Endomorphism.Setoid.html#898" class="Module">E</a> <a id="955" class="Keyword">hiding</a> <a id="962" class="Symbol">(</a><a id="963" href="Relation.Binary.Structures.html#1596" class="Function">refl</a><a id="967" class="Symbol">)</a>

<a id="970" class="Comment">------------------------------------------------------------------------</a>
<a id="1043" class="Comment">-- Basic type and functions</a>

<a id="Endo"></a><a id="1072" href="Function.Endomorphism.Setoid.html#1072" class="Function">Endo</a> <a id="1077" class="Symbol">:</a> <a id="1079" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1083" class="Symbol">_</a>
<a id="1085" href="Function.Endomorphism.Setoid.html#1072" class="Function">Endo</a> <a id="1090" class="Symbol">=</a> <a id="1092" href="Function.Endomorphism.Setoid.html#496" class="Bound">S</a> <a id="1094" href="Function.Equality.html#1254" class="Function Operator">⟶</a> <a id="1096" href="Function.Endomorphism.Setoid.html#496" class="Bound">S</a>

<a id="1099" class="Keyword">infixr</a> <a id="1106" class="Number">8</a> <a id="1108" href="Function.Endomorphism.Setoid.html#1113" class="Function Operator">_^_</a>

<a id="_^_"></a><a id="1113" href="Function.Endomorphism.Setoid.html#1113" class="Function Operator">_^_</a> <a id="1117" class="Symbol">:</a> <a id="1119" href="Function.Endomorphism.Setoid.html#1072" class="Function">Endo</a> <a id="1124" class="Symbol">→</a> <a id="1126" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1128" class="Symbol">→</a> <a id="1130" href="Function.Endomorphism.Setoid.html#1072" class="Function">Endo</a>
<a id="1135" href="Function.Endomorphism.Setoid.html#1135" class="Bound">f</a> <a id="1137" href="Function.Endomorphism.Setoid.html#1113" class="Function Operator">^</a> <a id="1139" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>  <a id="1145" class="Symbol">=</a> <a id="1147" href="Function.Equality.html#1464" class="Function">id</a>
<a id="1150" href="Function.Endomorphism.Setoid.html#1150" class="Bound">f</a> <a id="1152" href="Function.Endomorphism.Setoid.html#1113" class="Function Operator">^</a> <a id="1154" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1158" href="Function.Endomorphism.Setoid.html#1158" class="Bound">n</a> <a id="1160" class="Symbol">=</a> <a id="1162" href="Function.Endomorphism.Setoid.html#1150" class="Bound">f</a> <a id="1164" href="Function.Equality.html#1567" class="Function Operator">∘</a> <a id="1166" class="Symbol">(</a><a id="1167" href="Function.Endomorphism.Setoid.html#1150" class="Bound">f</a> <a id="1169" href="Function.Endomorphism.Setoid.html#1113" class="Function Operator">^</a> <a id="1171" href="Function.Endomorphism.Setoid.html#1158" class="Bound">n</a><a id="1172" class="Symbol">)</a>

<a id="^-cong₂"></a><a id="1175" href="Function.Endomorphism.Setoid.html#1175" class="Function">^-cong₂</a> <a id="1183" class="Symbol">:</a> <a id="1185" class="Symbol">∀</a> <a id="1187" href="Function.Endomorphism.Setoid.html#1187" class="Bound">f</a> <a id="1189" class="Symbol">→</a> <a id="1191" class="Symbol">(</a><a id="1192" href="Function.Endomorphism.Setoid.html#1187" class="Bound">f</a> <a id="1194" href="Function.Endomorphism.Setoid.html#1113" class="Function Operator">^_</a><a id="1196" class="Symbol">)</a> <a id="1198" href="Relation.Binary.Core.html#1577" class="Function Operator">Preserves</a> <a id="1208" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a> <a id="1212" href="Relation.Binary.Core.html#1577" class="Function Operator">⟶</a> <a id="1214" href="Relation.Binary.Bundles.html#1130" class="Function Operator">_≈_</a>
<a id="1218" href="Function.Endomorphism.Setoid.html#1175" class="Function">^-cong₂</a> <a id="1226" href="Function.Endomorphism.Setoid.html#1226" class="Bound">f</a> <a id="1228" class="Symbol">{</a><a id="1229" href="Function.Endomorphism.Setoid.html#1229" class="Bound">n</a><a id="1230" class="Symbol">}</a> <a id="1232" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a> <a id="1237" class="Symbol">=</a> <a id="1239" href="Function.Equality.html#1167" class="Field">cong</a> <a id="1244" class="Symbol">(</a><a id="1245" href="Function.Endomorphism.Setoid.html#1226" class="Bound">f</a> <a id="1247" href="Function.Endomorphism.Setoid.html#1113" class="Function Operator">^</a> <a id="1249" href="Function.Endomorphism.Setoid.html#1229" class="Bound">n</a><a id="1250" class="Symbol">)</a>

<a id="^-homo"></a><a id="1253" href="Function.Endomorphism.Setoid.html#1253" class="Function">^-homo</a> <a id="1260" class="Symbol">:</a> <a id="1262" class="Symbol">∀</a> <a id="1264" href="Function.Endomorphism.Setoid.html#1264" class="Bound">f</a> <a id="1266" class="Symbol">→</a> <a id="1268" href="Algebra.Morphism.Definitions.html#852" class="Function">Homomorphic₂</a> <a id="1281" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1283" href="Function.Endomorphism.Setoid.html#1072" class="Function">Endo</a> <a id="1288" href="Relation.Binary.Bundles.html#1130" class="Function Operator">_≈_</a> <a id="1292" class="Symbol">(</a><a id="1293" href="Function.Endomorphism.Setoid.html#1264" class="Bound">f</a> <a id="1295" href="Function.Endomorphism.Setoid.html#1113" class="Function Operator">^_</a><a id="1297" class="Symbol">)</a> <a id="1299" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">_+_</a> <a id="1303" href="Function.Equality.html#1567" class="Function Operator">_∘_</a>
<a id="1307" href="Function.Endomorphism.Setoid.html#1253" class="Function">^-homo</a> <a id="1314" href="Function.Endomorphism.Setoid.html#1314" class="Bound">f</a> <a id="1316" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>    <a id="1324" href="Function.Endomorphism.Setoid.html#1324" class="Bound">n</a> <a id="1326" href="Function.Endomorphism.Setoid.html#1326" class="Bound">x≈y</a> <a id="1330" class="Symbol">=</a> <a id="1332" href="Function.Equality.html#1167" class="Field">cong</a> <a id="1337" class="Symbol">(</a><a id="1338" href="Function.Endomorphism.Setoid.html#1314" class="Bound">f</a> <a id="1340" href="Function.Endomorphism.Setoid.html#1113" class="Function Operator">^</a> <a id="1342" href="Function.Endomorphism.Setoid.html#1324" class="Bound">n</a><a id="1343" class="Symbol">)</a> <a id="1345" href="Function.Endomorphism.Setoid.html#1326" class="Bound">x≈y</a>
<a id="1349" href="Function.Endomorphism.Setoid.html#1253" class="Function">^-homo</a> <a id="1356" href="Function.Endomorphism.Setoid.html#1356" class="Bound">f</a> <a id="1358" class="Symbol">(</a><a id="1359" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1363" href="Function.Endomorphism.Setoid.html#1363" class="Bound">m</a><a id="1364" class="Symbol">)</a> <a id="1366" href="Function.Endomorphism.Setoid.html#1366" class="Bound">n</a> <a id="1368" href="Function.Endomorphism.Setoid.html#1368" class="Bound">x≈y</a> <a id="1372" class="Symbol">=</a> <a id="1374" href="Function.Equality.html#1167" class="Field">cong</a> <a id="1379" href="Function.Endomorphism.Setoid.html#1356" class="Bound">f</a> <a id="1381" class="Symbol">(</a><a id="1382" href="Function.Endomorphism.Setoid.html#1253" class="Function">^-homo</a> <a id="1389" href="Function.Endomorphism.Setoid.html#1356" class="Bound">f</a> <a id="1391" href="Function.Endomorphism.Setoid.html#1363" class="Bound">m</a> <a id="1393" href="Function.Endomorphism.Setoid.html#1366" class="Bound">n</a> <a id="1395" href="Function.Endomorphism.Setoid.html#1368" class="Bound">x≈y</a><a id="1398" class="Symbol">)</a>

<a id="1401" class="Comment">------------------------------------------------------------------------</a>
<a id="1474" class="Comment">-- Structures</a>

<a id="∘-isMagma"></a><a id="1489" href="Function.Endomorphism.Setoid.html#1489" class="Function">∘-isMagma</a> <a id="1499" class="Symbol">:</a> <a id="1501" href="Algebra.Structures.html#1225" class="Record">IsMagma</a> <a id="1509" href="Relation.Binary.Bundles.html#1130" class="Function Operator">_≈_</a> <a id="1513" href="Function.Equality.html#1567" class="Function Operator">_∘_</a>
<a id="1517" href="Function.Endomorphism.Setoid.html#1489" class="Function">∘-isMagma</a> <a id="1527" class="Symbol">=</a> <a id="1529" class="Keyword">record</a>
  <a id="1538" class="Symbol">{</a> <a id="1540" href="Algebra.Structures.html#1277" class="Field">isEquivalence</a> <a id="1554" class="Symbol">=</a> <a id="1556" href="Relation.Binary.Bundles.html#1164" class="Function">isEquivalence</a>
  <a id="1572" class="Symbol">;</a> <a id="1574" href="Algebra.Structures.html#1315" class="Field">∙-cong</a>        <a id="1588" class="Symbol">=</a> <a id="1590" class="Symbol">λ</a> <a id="1592" href="Function.Endomorphism.Setoid.html#1592" class="Bound">g</a> <a id="1594" href="Function.Endomorphism.Setoid.html#1594" class="Bound">f</a> <a id="1596" href="Function.Endomorphism.Setoid.html#1596" class="Bound">x</a> <a id="1598" class="Symbol">→</a> <a id="1600" href="Function.Endomorphism.Setoid.html#1592" class="Bound">g</a> <a id="1602" class="Symbol">(</a><a id="1603" href="Function.Endomorphism.Setoid.html#1594" class="Bound">f</a> <a id="1605" href="Function.Endomorphism.Setoid.html#1596" class="Bound">x</a><a id="1606" class="Symbol">)</a>
  <a id="1610" class="Symbol">}</a>

<a id="∘-magma"></a><a id="1613" href="Function.Endomorphism.Setoid.html#1613" class="Function">∘-magma</a> <a id="1621" class="Symbol">:</a> <a id="1623" href="Algebra.Bundles.html#1123" class="Record">Magma</a> <a id="1629" class="Symbol">_</a> <a id="1631" class="Symbol">_</a>
<a id="1633" href="Function.Endomorphism.Setoid.html#1613" class="Function">∘-magma</a> <a id="1641" class="Symbol">=</a> <a id="1643" class="Keyword">record</a> <a id="1650" class="Symbol">{</a> <a id="1652" href="Algebra.Bundles.html#1275" class="Field">isMagma</a> <a id="1660" class="Symbol">=</a> <a id="1662" href="Function.Endomorphism.Setoid.html#1489" class="Function">∘-isMagma</a> <a id="1672" class="Symbol">}</a>

<a id="∘-isSemigroup"></a><a id="1675" href="Function.Endomorphism.Setoid.html#1675" class="Function">∘-isSemigroup</a> <a id="1689" class="Symbol">:</a> <a id="1691" href="Algebra.Structures.html#2897" class="Record">IsSemigroup</a> <a id="1703" href="Relation.Binary.Bundles.html#1130" class="Function Operator">_≈_</a> <a id="1707" href="Function.Equality.html#1567" class="Function Operator">_∘_</a>
<a id="1711" href="Function.Endomorphism.Setoid.html#1675" class="Function">∘-isSemigroup</a> <a id="1725" class="Symbol">=</a> <a id="1727" class="Keyword">record</a>
  <a id="1736" class="Symbol">{</a> <a id="1738" href="Algebra.Structures.html#2953" class="Field">isMagma</a> <a id="1746" class="Symbol">=</a> <a id="1748" href="Function.Endomorphism.Setoid.html#1489" class="Function">∘-isMagma</a>
  <a id="1760" class="Symbol">;</a> <a id="1762" href="Algebra.Structures.html#2977" class="Field">assoc</a>   <a id="1770" class="Symbol">=</a> <a id="1772" class="Symbol">λ</a> <a id="1774" href="Function.Endomorphism.Setoid.html#1774" class="Bound">h</a> <a id="1776" href="Function.Endomorphism.Setoid.html#1776" class="Bound">g</a> <a id="1778" href="Function.Endomorphism.Setoid.html#1778" class="Bound">f</a> <a id="1780" href="Function.Endomorphism.Setoid.html#1780" class="Bound">x≈y</a> <a id="1784" class="Symbol">→</a> <a id="1786" href="Function.Equality.html#1167" class="Field">cong</a> <a id="1791" href="Function.Endomorphism.Setoid.html#1774" class="Bound">h</a> <a id="1793" class="Symbol">(</a><a id="1794" href="Function.Equality.html#1167" class="Field">cong</a> <a id="1799" href="Function.Endomorphism.Setoid.html#1776" class="Bound">g</a> <a id="1801" class="Symbol">(</a><a id="1802" href="Function.Equality.html#1167" class="Field">cong</a> <a id="1807" href="Function.Endomorphism.Setoid.html#1778" class="Bound">f</a> <a id="1809" href="Function.Endomorphism.Setoid.html#1780" class="Bound">x≈y</a><a id="1812" class="Symbol">))</a>
  <a id="1817" class="Symbol">}</a>

<a id="∘-semigroup"></a><a id="1820" href="Function.Endomorphism.Setoid.html#1820" class="Function">∘-semigroup</a> <a id="1832" class="Symbol">:</a> <a id="1834" href="Algebra.Bundles.html#4121" class="Record">Semigroup</a> <a id="1844" class="Symbol">_</a> <a id="1846" class="Symbol">_</a>
<a id="1848" href="Function.Endomorphism.Setoid.html#1820" class="Function">∘-semigroup</a> <a id="1860" class="Symbol">=</a> <a id="1862" class="Keyword">record</a> <a id="1869" class="Symbol">{</a> <a id="1871" href="Algebra.Bundles.html#4289" class="Field">isSemigroup</a> <a id="1883" class="Symbol">=</a> <a id="1885" href="Function.Endomorphism.Setoid.html#1675" class="Function">∘-isSemigroup</a> <a id="1899" class="Symbol">}</a>

<a id="∘-id-isMonoid"></a><a id="1902" href="Function.Endomorphism.Setoid.html#1902" class="Function">∘-id-isMonoid</a> <a id="1916" class="Symbol">:</a> <a id="1918" href="Algebra.Structures.html#3974" class="Record">IsMonoid</a> <a id="1927" href="Relation.Binary.Bundles.html#1130" class="Function Operator">_≈_</a> <a id="1931" href="Function.Equality.html#1567" class="Function Operator">_∘_</a> <a id="1935" href="Function.Equality.html#1464" class="Function">id</a>
<a id="1938" href="Function.Endomorphism.Setoid.html#1902" class="Function">∘-id-isMonoid</a> <a id="1952" class="Symbol">=</a> <a id="1954" class="Keyword">record</a>
  <a id="1963" class="Symbol">{</a> <a id="1965" href="Algebra.Structures.html#4035" class="Field">isSemigroup</a> <a id="1977" class="Symbol">=</a> <a id="1979" href="Function.Endomorphism.Setoid.html#1675" class="Function">∘-isSemigroup</a>
  <a id="1995" class="Symbol">;</a> <a id="1997" href="Algebra.Structures.html#4067" class="Field">identity</a>    <a id="2009" class="Symbol">=</a> <a id="2011" href="Function.Equality.html#1167" class="Field">cong</a> <a id="2016" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2018" href="Function.Equality.html#1167" class="Field">cong</a>
  <a id="2025" class="Symbol">}</a>

<a id="∘-id-monoid"></a><a id="2028" href="Function.Endomorphism.Setoid.html#2028" class="Function">∘-id-monoid</a> <a id="2040" class="Symbol">:</a> <a id="2042" href="Algebra.Bundles.html#6033" class="Record">Monoid</a> <a id="2049" class="Symbol">_</a> <a id="2051" class="Symbol">_</a>
<a id="2053" href="Function.Endomorphism.Setoid.html#2028" class="Function">∘-id-monoid</a> <a id="2065" class="Symbol">=</a> <a id="2067" class="Keyword">record</a> <a id="2074" class="Symbol">{</a> <a id="2076" href="Algebra.Bundles.html#6212" class="Field">isMonoid</a> <a id="2085" class="Symbol">=</a> <a id="2087" href="Function.Endomorphism.Setoid.html#1902" class="Function">∘-id-isMonoid</a> <a id="2101" class="Symbol">}</a>

<a id="2104" class="Comment">------------------------------------------------------------------------</a>
<a id="2177" class="Comment">-- Homomorphism</a>

<a id="^-isSemigroupMorphism"></a><a id="2194" href="Function.Endomorphism.Setoid.html#2194" class="Function">^-isSemigroupMorphism</a> <a id="2216" class="Symbol">:</a> <a id="2218" class="Symbol">∀</a> <a id="2220" href="Function.Endomorphism.Setoid.html#2220" class="Bound">f</a> <a id="2222" class="Symbol">→</a> <a id="2224" href="Algebra.Morphism.html#1434" class="Record">IsSemigroupMorphism</a> <a id="2244" href="Data.Nat.Properties.html#16167" class="Function">+-semigroup</a> <a id="2256" href="Function.Endomorphism.Setoid.html#1820" class="Function">∘-semigroup</a> <a id="2268" class="Symbol">(</a><a id="2269" href="Function.Endomorphism.Setoid.html#2220" class="Bound">f</a> <a id="2271" href="Function.Endomorphism.Setoid.html#1113" class="Function Operator">^_</a><a id="2273" class="Symbol">)</a>
<a id="2275" href="Function.Endomorphism.Setoid.html#2194" class="Function">^-isSemigroupMorphism</a> <a id="2297" href="Function.Endomorphism.Setoid.html#2297" class="Bound">f</a> <a id="2299" class="Symbol">=</a> <a id="2301" class="Keyword">record</a>
  <a id="2310" class="Symbol">{</a> <a id="2312" href="Algebra.Morphism.html#1528" class="Field">⟦⟧-cong</a> <a id="2320" class="Symbol">=</a> <a id="2322" href="Function.Endomorphism.Setoid.html#1175" class="Function">^-cong₂</a> <a id="2330" href="Function.Endomorphism.Setoid.html#2297" class="Bound">f</a>
  <a id="2334" class="Symbol">;</a> <a id="2336" href="Algebra.Morphism.html#1572" class="Field">∙-homo</a>  <a id="2344" class="Symbol">=</a> <a id="2346" href="Function.Endomorphism.Setoid.html#1253" class="Function">^-homo</a> <a id="2353" href="Function.Endomorphism.Setoid.html#2297" class="Bound">f</a>
  <a id="2357" class="Symbol">}</a>

<a id="^-isMonoidMorphism"></a><a id="2360" href="Function.Endomorphism.Setoid.html#2360" class="Function">^-isMonoidMorphism</a> <a id="2379" class="Symbol">:</a> <a id="2381" class="Symbol">∀</a> <a id="2383" href="Function.Endomorphism.Setoid.html#2383" class="Bound">f</a> <a id="2385" class="Symbol">→</a> <a id="2387" href="Algebra.Morphism.html#1946" class="Record">IsMonoidMorphism</a> <a id="2404" href="Data.Nat.Properties.html#16398" class="Function">+-0-monoid</a> <a id="2415" href="Function.Endomorphism.Setoid.html#2028" class="Function">∘-id-monoid</a> <a id="2427" class="Symbol">(</a><a id="2428" href="Function.Endomorphism.Setoid.html#2383" class="Bound">f</a> <a id="2430" href="Function.Endomorphism.Setoid.html#1113" class="Function Operator">^_</a><a id="2432" class="Symbol">)</a>
<a id="2434" href="Function.Endomorphism.Setoid.html#2360" class="Function">^-isMonoidMorphism</a> <a id="2453" href="Function.Endomorphism.Setoid.html#2453" class="Bound">f</a> <a id="2455" class="Symbol">=</a> <a id="2457" class="Keyword">record</a>
  <a id="2466" class="Symbol">{</a> <a id="2468" href="Algebra.Morphism.html#2037" class="Field">sm-homo</a> <a id="2476" class="Symbol">=</a> <a id="2478" href="Function.Endomorphism.Setoid.html#2194" class="Function">^-isSemigroupMorphism</a> <a id="2500" href="Function.Endomorphism.Setoid.html#2453" class="Bound">f</a>
  <a id="2504" class="Symbol">;</a> <a id="2506" href="Algebra.Morphism.html#2101" class="Field">ε-homo</a>  <a id="2514" class="Symbol">=</a> <a id="2516" class="Symbol">λ</a> <a id="2518" href="Function.Endomorphism.Setoid.html#2518" class="Bound">x≈y</a> <a id="2522" class="Symbol">→</a> <a id="2524" href="Function.Endomorphism.Setoid.html#2518" class="Bound">x≈y</a>
  <a id="2530" class="Symbol">}</a>
</pre></body></html>